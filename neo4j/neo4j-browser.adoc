## Présentation Neo4J Browser

//
// https://neo4j.com/docs/cypher-refcard/current/
// Présenter les fonctionnalités qui ne sont pas supportés par Graphgist
// Créer des favoris / ou un répertoire de requêtes sur Neo4J Browser

TODO : MAJ date début transaction frauduleuse (juin et non mars)
TODO : Créer learning expedition comme evenement au mois de mai
TODO : Date ouverture de compte


## Génération d'un graphe modele

[source,cypher]
```
// 01 - extraction modele
MATCH (a)-[r]->(b) WHERE labels(a) <> [] AND labels(b) <> []
RETURN DISTINCT head(labels(a)) AS This, type(r) as To, head(labels(b)) AS That order by This, To, That LIMIT 100
```

## Graphe contenant jusqu'à la partie blacklist de pays

[source,cypher]
----
// 02 - renvoyer toute la base
match(n) return n
----


## Liste des transactions et de leurs relations +1

[source,cypher]
----
// 03 - Transactions + relation directe
match (n:Transaction)--(o:Compte) return n, o
----

## Transactions vers des comptes étrangers (à partir de l'iban)

[source,cypher]
----
// 04 - Transactions vers des comptes étrangers
match (t:Transaction)--(c:Compte)
where NOT t.ibanDestinataire STARTS WITH 'FR'
return t,c
----


## Requête permettant de trouver les requêtes frauduleuses (pays de la banque sur blacklist)

[source,cypher]
```
// 05 - Requetes frauduleuses
MATCH path =(t:Transaction)-[*..4]-(BlackList{flagBlackList:true})
RETURN nodes(path)
```

## Requête permettant de trouver la localisation des requêtes frauduleuses

TODO : à faire Extraction des coordonnées GPS
[source,cypher]
```
// 06 - Localisation Position Requetes frauduleuses
MATCH path =(p:Position)--(t:Transaction)-[*..4]-(BlackList{flagBlackList:true})
RETURN nodes(path)
```


## Requête permettant de filtrer les consultants arrivés avant les transactions frauduleuses

// documentation conversion date :
// https://neo4j-contrib.github.io/neo4j-apoc-procedures/#_date_and_time_conversions

[source,cypher]
```
// 07 - Consultants deja embauchés
MATCH path =(p:Position)--(t:Transaction)-[*..4]-(BlackList{flagBlackList:true})
WITH t
MATCH (c:Consultant)--(p:Personne)
WHERE apoc.date.parse(c.dateEmbauche,'s','yyyy-MM-dd') < apoc.date.parse('2017-06-01','s','yyyy-MM-dd')
RETURN DISTINCT id(p), p.prenom, p.nom, c.dateEmbauche
```


## Requête permettant de filtrer les consultants à l'agence pendant transactions frauduleuses

[source,cypher]
```
// 08 - Consultants à l'agence pendant les transactions frauduleuses
MATCH (t:Transaction)-[*..4]-(BlackList{flagBlackList:true})
WITH collect(distinct t.date) as ts
MATCH path = (pers:Personne)--(:Consultant)-[work:TRAVAILLE]->(a:Agence{libelle:'Zenika Nantes'})
WHERE all(x in ts WHERE x in work.dates)
return nodes(path)
```
// --> Olivier

## Requête permettant de trouver les consultants à l'étranger à l'ouverture du compte

[source,cypher]
```
// 09 - Consultants à l'étranger lors de l'ouverture du compte
MATCH pathT = (Transaction)-[:VERS]-(c:Compte)-[*..4]-(BlackList{flagBlackList:true})
WITH c, pathT
MATCH path=(p:Personne)-[*..2]-(evt:Evt)
WHERE evt.pays <> 'France'
and apoc.date.parse(c.dateOuverture,'s','yyyy-MM-dd') >= apoc.date.parse(evt.dateDebut,'s','yyyy-MM-dd')
and apoc.date.parse(c.dateOuverture,'s','yyyy-MM-dd') <= apoc.date.parse(evt.dateFin,'s','yyyy-MM-dd')
RETURN nodes(path)//, nodes (pathT)
// --> Julien


### Ajout des noeuds photos + localisation

TODO
[source,cypher]
```
// 10 - Ajout des photos et de leurs metadatas
```

### Distance entre deux localisation

.Distances entre deux points
[source,cypher]
----
//11 - Distance entre 2 positions (en km)
Match(pos1:Position {site: 'Zenika'})
Match (pos2:Position {site: 'Zenika Paris'})
WITH point({ longitude: pos1.longitude, latitude: pos1.latitude }) AS p1, point({ longitude: pos2.longitude, latitude: pos2.latitude }) AS p2
RETURN distance(p1,p2)/1000 AS dist
----

### Relation proche entre la banque et les autres localisations

.Créer une relation entre chaque lieu contenant leur distance
[source,cypher]
----
//12 - Creation d'une relation "PROCHE" < 10km
Match(pos1:Position{site:"banque Panama"}), (pos2:Position)
WHERE pos1.site <> pos2.site
WITH point({ longitude: pos1.longitude, latitude: pos1.latitude }) AS p1, point({ longitude: pos2.longitude, latitude: pos2.latitude }) AS p2, pos1, pos2
WITH distance(p1,p2)/1000 as distance, pos1, pos2
WHERE distance < 10
MERGE (pos1)-[:PROCHE{valeur:distance}]-(pos2)
----



### Requête : trouver qui est lié à la banque

[source,cypher]
----
//13 Qui est lié à la banque
Match path = (p:Personne)-[*..6]-(l:Position{site:"banque Panama"})
return  nodes(path)
ORDER BY LENGTH(path) limit 1
----





### Aller plus loin
https://neo4j-contrib.github.io/neo4j-apoc-procedures/


## Commandes utilitaires pour nous

### Exporter ses favoris depuis le browser
Source : https://neo4j.com/developer/kb/how-do-i-export-cypher-favorites-recorded-in-the-browser/

[source,javascript]
----
var res = JSON.parse(localStorage.getItem('neo4j.documents'))
for (x in res) { console.log(res[x]['content']) }
----



### Supprimer le contenu de la base
[source,cypher]
----
MATCH (n)
DETACH DELETE n
----

### Renvoyer tout le contenu de la base
[source,cypher]
----
MATCH (n)
RETURN n
----

## Génération d'un graphe modele

[source,cypher]
```
// extraction modele
MATCH (a)-[r]->(b) WHERE labels(a) <> [] AND labels(b) <> []
RETURN DISTINCT head(labels(a)) AS This, type(r) as To, head(labels(b)) AS That LIMIT 100
```

[source,cypher]
```
 // generate the META-graph
 MATCH (a)-[r]->(b)
 WITH labels(a) AS a_labels,type(r) AS rel_type,labels(b) AS b_labels
 UNWIND a_labels as l
 UNWIND b_labels as l2
 MERGE (a:Node:Meta {name:l})
 MERGE (b:Node:Meta {name:l2})
 MERGE (a)-[:OUTGOING]->(:Relationship:Meta {name:rel_type})-[:INCOMING]->(b)
 RETURN distinct l as first_node, rel_type as connected_by, l2 as second_node
```

Puis faire `match(n:Node)--(m) return n,m`